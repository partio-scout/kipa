*** Settings ***
Documentation   Testataan perustoiminnallisuudet
Suite Setup     Kipa Suite Setup
Suite Teardown  Stop Selenium Server
Test Setup      Open Competition
Test Teardown   Close Browser
Force Tags      regression  smoke
Library         SeleniumLibrary

*** Variables ***
${BROWSER}  Firefox
${KIPA_URL}  ${EMPTY}
${KISAN_MAARITYS_URL}  ${EMPTY}
${TESTIKISA}  testikisa

*** Test Cases ***
Luodaan uusi kisa
    [Setup]  Open Kipa Main Page
    Create new Competition  ${TESTIKISA}

Tarkista syötä suorituksia
    Open Sub Page Verify Location And Title  syötä suorituksia  ${TULOSTEN_SYOTTO_URL}  Kipa - Syötä tuloksia

Tarkista syötä suorituksia - tarkistus
    Open Sub Page Verify Location And Title  syötä suorituksia - tarkistus  ${TULOSTEN_SYOTTO_TARKISTUS_URL}  Kipa - Syötä tuloksia

Tarkista tulossivu
    Open Sub Page Verify Location And Title  sarjan tulokset  ${TULOSTEN_TARKISTUS_URL}  ${TULOSTEN_TARKISTUS_OTSIKKO}

Tarkista Tuomarineuvosto
    Open Sub Page Verify Location And Title  tuomarineuvoston työkalu  ${TUOMARINEUVOSTO_URL}  Kipa - Tuomarineuvoston antamien tulosten määritys
    Page Should Contain Button  Peruuta
    Page Should Contain Button  tallenna

Tarkista laskennan tilanne
    Open Sub Page Verify Location And Title  laskennan tilanne  ${LASKENNAN_TILANNE_URL}  ${LASKENNAN_TILANNE_OTSIKKO}

Tarkista vartioiden määritys
    Open Sub Page Verify Location And Title  määrittele vartiot  ${VARTIOIDEN_MAARITYS_URL}  Kipa - Määritä vartiot
    Page Should Contain Button  Peruuta
    Page Should Contain Button  tallenna

Tarkista tehtävien määritys
    Open Sub Page Verify Location And Title  määrittele tehtävät  ${TEHTAVAN_MAARITYS_URL}  Kipa - Muokkaa tehtävää

Tarkista Testituloksien määritys
    Open Sub Page Verify Location And Title  määrittele testituloksia  ${TESTITULOKSIEN_MAARITYS_URL}  Kipa - Testituloksien määritys
    Page Should Contain Button  Peruuta
    Page Should Contain Button  tallenna

Tarkista palauta kisa
    Open Sub Page Verify Location And Title  palauta kisa  ${KISAN_TUONTI_URL}  ${KISAN_TUONTI_OTSIKKO}
    Page Should Contain Button  Peruuta
    Page Should Contain Button  tallenna
    Page Should Contain Element  file

Tarkista sarjan tulokset
    Open Sub Page Verify Location And Title  sarjan tulokset  ${TULOSTEN_TARKISTUS_URL}  ${TULOSTEN_TARKISTUS_OTSIKKO}
    Page Should Contain Link  Heijasta tuloksia

Poista Luotu Kisa
    Remove Competition  ${TESTIKISA}

*** Keywords ***
Open KiPa Main Page
    Open Browser And Maximize  ${KIPA_URL}/
    Maximize Browser Window
    Title Should Be  ${KIPA_OTSIKKO}

Create New Competition
    [Arguments]  ${competition}
    [Documentation]  Creates new competition and checks that it exist. Expects TESTIKISA to be given as argument.
    Click Link  Luo uusi kisa
    Location Should Be  ${KIPA_URL}/${KISAN_MAARITYS_URL}/
    Input Text  id_nimi  ${competition}
    Click Button  tallenna
    Close Browser
    Open Browser And Maximize  ${KIPA_URL}/
    Click Link  ${competition}
    Location Should Be  ${KIPA_URL}/${competition}/
    Title Should Be  Kipa - ${competition}

Open Browser and Maximize
    [Arguments]  ${page}
    Open Browser  ${page}  ${BROWSER}
    Maximize Browser Window

Kipa Suite Setup
    Start Selenium Server
    Open Kipa Main Page
    Close Browser

Open Sub Page Verify Location And Title
    [Arguments]  ${link_name}  ${link}  ${page_title}
    Click Link  ${link_name}
    Location Should Be  ${KIPA_URL}/${TESTIKISA}/${link}/
    Title Should Be  ${page_title}

Open Competition
    Open KiPa Main Page
    Click Link  ${TESTIKISA}
    Title Should Be  Kipa - ${TESTIKISA}

Remove Competition
    [Arguments]  ${competition}
    Open KiPa Main Page
    click link  ${competition}
    title should be  Kipa - ${competition}
    Click Link  poista kisa
    Title Should Be  ${KISAN_POISTO_OTSIKKO}
    Click Button  Kyllä
    sleep  2 sec
    Title Should Be  Kipa - kaikki kisat
    Location Should Be  ${KIPA_URL}/

